plugins {
    id 'checkstyle'
    id 'java'
    id 'nu.studer.jooq' version '8.2'
}

group 'net.dumbdogdiner'
version '1.0.1'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.dmulloy2.net/repository/public/' }
}

final var commandApiVersion = '9.0.3'
final var jooqVersion = '3.18.5'

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'

    // commands
    compileOnly "dev.jorel:commandapi-bukkit-core:${commandApiVersion}"
    compileOnly "dev.jorel:commandapi-annotations:${commandApiVersion}"
    annotationProcessor "dev.jorel:commandapi-annotations:${commandApiVersion}"

    // to allow other plugins to interface with economy
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    // database
    compileOnly 'org.postgresql:postgresql:42.6.0'
    compileOnly "org.jooq:jooq:${jooqVersion}"
    compileOnly 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

    // permissions
    compileOnly 'net.luckperms:api:5.4'

    // hide insecure chat popup
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.0.0'

    jooqGenerator "org.jooq:jooq-meta-extensions:${jooqVersion}"
}

jooq {
    version = jooqVersion

    configurations {
        main {
            generationTool {
                generator {
                    database {
                        name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                        properties {
                            property {
                                key = 'scripts'
                                value = 'src/main/resources/database.sql'
                            }
                        }
                    }
                    target {
                        packageName = 'net.dumbdogdiner.dogcore.database.schema'
                    }
                }
            }
        }
    }
}

processResources {
    filesMatching("plugin.yml") {
        filter { it.replace('$VERSION', version) }
    }
}

checkstyle {
    // tell checkstyle to only check our source code, not the generated files
    checkstyleMain.source = 'src/main/java'
}
